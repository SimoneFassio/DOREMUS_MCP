# category: "Works and Expressions"
# question: "Give me the works of chamber music that involves at most violin, clarinet and piano, except from the sonatas for violin and piano and clarinet and piano"
# query:
SELECT DISTINCT ?expression, SAMPLE(?title) as ?title, ?groupCount AS ?nbInstruments, ?isViolin, ?isClarinet, ?isPiano, ?casting
WHERE {
  ?expression a efrbroo:F22_Self-Contained_Expression ;
          rdfs:label ?title ;
          mus:U13_has_casting ?casting ;
          mus:U12_has_genre / skos:prefLabel ?genre.
  FILTER contains(str(?genre), "chambre") .

  {
    SELECT DISTINCT ?casting ?groupCount ?isViolin ?isClarinet ?isPiano
    WHERE {
      ?casting mus:U23_has_casting_detail ?allCastingDets .
      OPTIONAL {
        ?casting mus:U23_has_casting_detail ?castingDet1 .
        ?castingDet1 mus:U2_foresees_use_of_medium_of_performance ?violin .
        VALUES (?violin) { ( <http://data.doremus.org/vocabulary/iaml/mop/svl> ) (<http://www.mimo-db.eu/InstrumentsKeywords/3573>) }
      }
      BIND(IF(BOUND(?castingDet1), 1, 0) AS ?isViolin )

      OPTIONAL {
        ?casting mus:U23_has_casting_detail ?castingDet2 .
        ?castingDet2 mus:U2_foresees_use_of_medium_of_performance ?clarinet .
        VALUES (?clarinet) { (<http://data.doremus.org/vocabulary/iaml/mop/wcl>) (<http://www.mimo-db.eu/InstrumentsKeywords/3836>) }
      }
      BIND(IF(BOUND(?castingDet2), 1, 0) AS ?isClarinet )

      OPTIONAL {
        ?casting mus:U23_has_casting_detail ?castingDet3 .
        ?castingDet3 mus:U2_foresees_use_of_medium_of_performance ?piano .
        VALUES (?piano) { (<http://data.doremus.org/vocabulary/iaml/mop/kpf>) (<http://www.mimo-db.eu/InstrumentsKeywords/2299>) }
      }
      BIND(IF(BOUND(?castingDet3), 1, 0) AS ?isPiano )

      BIND((?isViolin + ?isClarinet + ?isPiano ) AS ?groupCount)

    }
    GROUP BY ?casting ?groupCount ?isViolin ?isClarinet ?isPiano
    HAVING (?groupCount > 0 AND ?groupCount = count(?allCastingDets))
  }

  MINUS {
    ?expression a efrbroo:F22_Self-Contained_Expression ;
            mus:U13_has_casting ?casting ;
            mus:U12_has_genre <http://data.doremus.org/vocabulary/iaml/genre/sn> .
    {
      ?casting mus:U23_has_casting_detail ?castingDet1 .
      ?castingDet1 mus:U2_foresees_use_of_medium_of_performance ?violin .
      VALUES (?violin) { ( <http://data.doremus.org/vocabulary/iaml/mop/svl> ) (<http://www.mimo-db.eu/InstrumentsKeywords/3573>) }
    } UNION {
      ?casting mus:U23_has_casting_detail ?castingDet2 .
      ?castingDet2 mus:U2_foresees_use_of_medium_of_performance ?clarinet .
      VALUES (?clarinet) { (<http://data.doremus.org/vocabulary/iaml/mop/wcl>) (<http://www.mimo-db.eu/InstrumentsKeywords/3836>) }
    }

    ?casting mus:U23_has_casting_detail ?castingDet3 .
    ?castingDet3 mus:U2_foresees_use_of_medium_of_performance ?piano .
    VALUES (?piano) { (<http://data.doremus.org/vocabulary/iaml/mop/kpf>) (<http://www.mimo-db.eu/InstrumentsKeywords/2299>) }
  }
} ORDER BY desc(?nbInstruments)
