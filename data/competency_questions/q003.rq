# category: "Works and Expressions"
# question: "Which works have been written for string quartet?"
# build_query(question="Which works have been written for string quartet?", template="Works", filters={})
# associate_to_N_entities(subject="expression", obj="violin", query_id=query_id, n=2)
# associate_to_N_entities(subject="expression", obj="viola", query_id=query_id, n=1)
# associate_to_N_entities(subject="expression", obj="cello", query_id=query_id, n=1)
# groupBy_having(subject="casting", query_id=query_id, obj="castingDetail", function="COUNT", logic_type="equal", valueStart="3")
# query:
SELECT DISTINCT ?expression, SAMPLE(?title) as ?title, ?casting
WHERE {
  ?expression a efrbroo:F22_Self-Contained_Expression ;
          rdfs:label ?title ;
          mus:U13_has_casting ?casting .

    ?casting mus:U23_has_casting_detail ?allCastingDets .

    ?casting mus:U23_has_casting_detail ?castingDet1 .
    ?castingDet1 mus:U2_foresees_use_of_medium_of_performance ?violin ;
            mus:U30_foresees_quantity_of_mop 2 .


    ?casting mus:U23_has_casting_detail ?castingDet2 .
    ?castingDet2 mus:U2_foresees_use_of_medium_of_performance ?viola ;
            mus:U30_foresees_quantity_of_mop 1 .

    ?casting mus:U23_has_casting_detail ?castingDet3 .
    ?castingDet3 mus:U2_foresees_use_of_medium_of_performance ?cello ;
            mus:U30_foresees_quantity_of_mop 1 .

  VALUES (?violin) { ( <http://data.doremus.org/vocabulary/iaml/mop/svl> ) (<http://www.mimo-db.eu/InstrumentsKeywords/3573>) }
  VALUES (?viola) { (<http://data.doremus.org/vocabulary/iaml/mop/sva>) (<http://www.mimo-db.eu/InstrumentsKeywords/3561>) }
  VALUES (?cello) { (<http://data.doremus.org/vocabulary/iaml/mop/svc>) (<http://www.mimo-db.eu/InstrumentsKeywords/3582>) }

}
GROUP BY ?expression ?casting
HAVING (COUNT(?allCastingDets) = 3)
