# category: "Works and Expressions"
# question: "Give me all the works for piano connected to other musical works"
# query:
SELECT DISTINCT ?work, SAMPLE(?title) as ?title, ?link, ?work2, SAMPLE(?title2) as ?title2
WHERE {
  ?work ?link ?work2.

  ?work a efrbroo:F14_Individual_Work;
        efrbroo:R9_is_realised_in ?expression .

  ?expression rdfs:label ?title ;
        mus:U13_has_casting ?casting .

  { SELECT ?casting
    WHERE {
    ?casting mus:U23_has_casting_detail / mus:U2_foresees_use_of_medium_of_performance / skos:exactMatch* <http://data.doremus.org/vocabulary/iaml/mop/kpf> ;
          mus:U23_has_casting_detail ?cDet .
    } GROUP BY ?casting
    HAVING (COUNT(?cDet) = 1)
  }

  ?work2 a efrbroo:F14_Individual_Work;
        efrbroo:R9_is_realised_in / rdfs:label ?title2 .
}
ORDER BY ?title

# Note: so far, we have in the database only link of type ecrm:P148_has_component
