# category: "Works and Expressions"
# question: "Give me all works for piano based on works of Schubert"
# query:
SELECT ?work AS ?original (SAMPLE(?title) AS ?original_title) ?work2 AS ?derived (SAMPLE(?title2) AS ?derived_title)
WHERE {
  ?expression a efrbroo:F22_Self-Contained_Expression ;
              rdfs:label ?title ;
              mus:U13_has_casting ?casting .

  ?expCreation efrbroo:R17_created ?expression ;
               ecrm:P9_consists_of ?activity .
  ?activity ecrm:P14_carried_out_by <http://data.doremus.org/artist/03954109-0253-35d6-a70e-89ab27dea09c> .

  ?work efrbroo:R9_is_realised_in ?expression ;
        a efrbroo:F14_Individual_Work ;
        efrbroo:R2_is_derivative_of ?work2 .
  ?work2 a efrbroo:F14_Individual_Work ;
         efrbroo:R9_is_realised_in ?expression2 .
  ?expression2 rdfs:label ?title2 .

  ?casting mus:U23_has_casting_detail ?cDet .
  ?cDet mus:U2_foresees_use_of_medium_of_performance / skos:exactMatch* <http://data.doremus.org/vocabulary/iaml/mop/kpf> .
}
GROUP BY ?work ?work2 ?casting ?expression
HAVING (COUNT(DISTINCT ?cDet) = 1)
