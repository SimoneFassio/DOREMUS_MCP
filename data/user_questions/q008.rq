# category: "Works and Expressions"
# question: "Which works of Beethoven have a dedication statement to a prince?"
# query:
SELECT DISTINCT ?expression ?title ?dedicationComment
WHERE {
  ?expression a efrbroo:F22_Self-Contained_Expression ;
          rdfs:label ?title ;
          mus:U44_has_dedication_statement ?dedication .
  ?dedication ecrm:P3_has_note ?dedicationComment .
  ?expCreation efrbroo:R17_created ?expression ;
    ecrm:P9_consists_of ?compositionActivity .
  ?compositionActivity ecrm:P14_carried_out_by ?composer ;
    mus:U31_had_function <http://data.doremus.org/vocabulary/function/composer> .
  ?composer foaf:name "Ludwig van Beethoven" .
  FILTER (CONTAINS(LCASE(?dedicationComment), "prince"))
}
