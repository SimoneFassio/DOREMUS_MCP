# category: "Works and Expressions"
# question: "Show me the Operas composed by Giuseppe Verdi in 1853."
# query:
SELECT DISTINCT ?expression ?title
WHERE {
  ?expression a efrbroo:F22_Self-Contained_Expression ;
          rdfs:label ?title ;
          mus:U12_has_genre <http://data.doremus.org/vocabulary/iaml/genre/op> .
  ?expCreation efrbroo:R17_created ?expression ;
    ecrm:P9_consists_of ?compositionActivity .
  ?compositionActivity ecrm:P14_carried_out_by ?composer ;
    mus:U31_had_function <http://data.doremus.org/vocabulary/function/composer> .
  ?composer foaf:name "Giuseppe Verdi" .
  ?expCreation ecrm:P4_has_time-span / time:hasBeginning / time:inXSDDate ?start ;
    ecrm:P4_has_time-span / time:hasEnd / time:inXSDDate ?end .
  FILTER (?start >= "1853"^^xsd:gYear && ?end <= "1853"^^xsd:gYear)
}
